From 7be3f9735c5b7090e20c1a316e0398d3f6ec5f5e Mon Sep 17 00:00:00 2001
From: tfrench <tfrench@uber.com>
Date: Wed, 27 Sep 2023 13:50:59 +0000
Subject: [PATCH] feat(bazel): make googleapis a Bzlmod module

---
 MODULE.bazel     | 22 +++++++++++++++++++++
 WORKSPACE.bzlmod |  2 ++
 extensions.bzl   | 50 ++++++++++++++++++++++++++++++++++++++++++++++++
 3 files changed, 74 insertions(+)
 create mode 100644 MODULE.bazel
 create mode 100644 WORKSPACE.bzlmod
 create mode 100644 extensions.bzl

diff --git a/MODULE.bazel b/MODULE.bazel
new file mode 100644
index 000000000000..a35b9c2d2997
--- /dev/null
+++ b/MODULE.bazel
@@ -0,0 +1,22 @@
+module(
+    name = "com_google_googleapis",
+    version = "0.0.0",
+)
+
+bazel_dep(name = "protobuf", version = "21.7", repo_name = "com_google_protobuf")
+bazel_dep(name = "rules_proto", version = "5.3.0-21.7")
+
+switched_rules = use_extension("//:extensions.bzl", "switched_rules")
+switched_rules.use_languages(
+    cc = True,
+    csharp = True,
+    gapic = True,
+    go = True,
+    grpc = True,
+    java = True,
+    nodejs = True,
+    php = True,
+    python = True,
+    ruby = True,
+)
+use_repo(switched_rules, "com_google_googleapis_imports")
diff --git a/WORKSPACE.bzlmod b/WORKSPACE.bzlmod
new file mode 100644
index 000000000000..fafa28da3459
--- /dev/null
+++ b/WORKSPACE.bzlmod
@@ -0,0 +1,2 @@
+# When Bzlmod is enabled, this file replaces the content of the original WORKSPACE
+# and makes sure no WORKSPACE prefix or suffix are added when Bzlmod is enabled.
diff --git a/extensions.bzl b/extensions.bzl
new file mode 100644
index 000000000000..d8f04cf6c7cc
--- /dev/null
+++ b/extensions.bzl
@@ -0,0 +1,50 @@
+load(":repository_rules.bzl", "switched_rules_by_language")
+
+_use_languages_tag = tag_class(
+    attrs = {
+        "cc": attr.bool(default = False),
+        "csharp": attr.bool(default = False),
+        "gapic": attr.bool(default = False),
+        "go": attr.bool(default = False),
+        "go_test": attr.bool(default = False),
+        "grpc": attr.bool(default = False),
+        "java": attr.bool(default = False),
+        "nodejs": attr.bool(default = False),
+        "php": attr.bool(default = False),
+        "python": attr.bool(default = False),
+        "ruby": attr.bool(default = False),
+    },
+)
+
+def _switched_rules_impl(ctx):
+    attrs = {}
+    for module in ctx.modules:
+        if not module.is_root:
+            continue
+
+        for t in module.tags.use_languages:
+            attrs = {
+                "cc": t.cc,
+                "csharp": t.csharp,
+                "gapic": t.gapic,
+                "go": t.go,
+                "go_test": t.go_test,
+                "grpc": t.grpc,
+                "java": t.java,
+                "nodejs": t.nodejs,
+                "php": t.php,
+                "python": t.python,
+                "ruby": t.ruby,
+            }
+
+    switched_rules_by_language(
+        name = "com_google_googleapis_imports",
+        **attrs
+    )
+
+switched_rules = module_extension(
+    implementation = _switched_rules_impl,
+    tag_classes = {
+        "use_languages": _use_languages_tag,
+    },
+)
