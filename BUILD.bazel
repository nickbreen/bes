load("@rules_jvm_external//:defs.bzl", "artifact")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

oci_push(
    name = "bes.push",
    image = ":bes.img",
    remote_tags = ["latest"],
    repository = "ghcr.io/nickbreen/bes",
)

# Note: the docker snap has access to ~, but not hidden files/dirs, so it
# cannot see ~/.cache which is where bazel-bin/bes.load/tarball.tar really is.
# So when using the docker snap, one must load it via stdin:
#     docker load < bazel-bin/bes.load/tarball.tar
# Rather than with:
#     bazel run bes.load
oci_tarball(
    name = "bes.load",
    image = ":bes.img",
    repo_tags = ["ghcr.io/nickbreen/bes:latest"],
)

oci_image(
    name = "bes.img",
    base = "@jre",
    cmd = [],
    entrypoint = [
        "java",
        "-jar",
        "bes_deploy.jar",
    ],
    tars = [":bes.tar"],
)

pkg_tar(
    name = "bes.tar",
    srcs = [":bes_deploy.jar"],
)

java_binary(
    name = "bes",
    main_class = "nickbreen.bes.BesServer",
    runtime_deps = [
        ":main",
        artifact("org.xerial:sqlite-jdbc"),
        artifact("com.mysql:mysql-connector-j"),
        artifact("org.mariadb.jdbc:mariadb-java-client"),
        artifact("org.postgresql:postgresql"),
    ],
)

java_binary(
    name = "bec",
    main_class = "nickbreen.bes.BesClient",
    runtime_deps = [":main"],
)

java_library(
    name = "main",
    srcs = glob(["src/main/java/**/*.java"]),
    resources = glob(["src/main/resources/**"]),
    runtime_deps = [
        "@io_grpc_grpc_java//netty",
    ],
    deps = [
        ":domain",
        ":support",
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_grpc",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        "@io_grpc_grpc_java//api",
        "@io_grpc_grpc_java//stub",
        artifact("com.google.protobuf:protobuf-java-util"),
        artifact("org.jcommander:jcommander"),
        artifact("com.mchange:c3p0"),
    ],
)

java_library(
    name = "domain",
    srcs = glob(["src/domain/java/**/*.java"]),
    resources = glob(["src/domain/resources/**"]),
    deps = [
        ":support",
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        artifact("com.google.protobuf:protobuf-java-util"),
    ],
)

java_library(
    name = "support",
    srcs = glob(["src/support/java/**/*.java"]),
    resources = glob(["src/support/resources/**"]),
    deps = [
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        artifact("com.google.protobuf:protobuf-java-util"),
    ],
)

java_library(
    name = "test-support",
    testonly = True,
    srcs = glob(["src/test-support/java/**/*.java"]),
    resources = glob(["src/test-support/resources/**"]),
    deps = [
        ":support",
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        artifact("com.google.protobuf:protobuf-java-util"),
        artifact("org.hamcrest:hamcrest"),
    ],
)

java_test(
    name = "test",
    size = "small",
    srcs = glob(["src/test/java/**/*.java"]),
    resources = glob(["src/test/resources/**"]),
    test_class = "nickbreen.bes.TestSuite",
    runtime_deps = [
        artifact("org.glassfish:javax-json"),
    ],
    deps = [
        ":domain",
        ":test-support",
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        artifact("javax.json:javax.json-api"),
        artifact("junit:junit"),
        artifact("org.hamcrest:hamcrest"),
    ],
)

java_test(
    name = "integration-test",
    size = "medium",
    srcs = glob(["src/integration-test/java/**/*.java"]),
    data = glob(["src/integration-test/resources/**"]),
    resources = glob(["src/integration-test/resources/**"]),
    tags = [
        "no-sandbox",  # need to talk to docker socket
    ],
    test_class = "nickbreen.bes.IntegrationTestSuite",
    runtime_deps = [
        artifact("org.xerial:sqlite-jdbc"),
        artifact("com.mysql:mysql-connector-j"),
        artifact("org.mariadb.jdbc:mariadb-java-client"),
        artifact("org.postgresql:postgresql"),
        artifact("org.testcontainers:mariadb"),
        artifact("org.testcontainers:mysql"),
        artifact("org.testcontainers:postgresql"),
        "@io_grpc_grpc_java//api",
    ],
    deps = [
        ":domain",
        ":main",
        ":test-support",
        "@bazel//src/main/java/com/google/devtools/build/lib/buildeventstream/proto:build_event_stream_java_proto",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        artifact("com.google.protobuf:protobuf-java-util"),
        artifact("junit:junit"),
        artifact("org.hamcrest:hamcrest"),
        artifact("org.testcontainers:testcontainers"),
    ],
)

java_test(
    name = "acceptance-test",
    srcs = glob(["src/acceptance-test/java/**"]),
    data = [
        ":bec_deploy.jar",
        ":bes_deploy.jar",
    ],
    jvm_flags = [
        "-Dbec.uber.jar=$(rootpath :bec_deploy.jar)",
        "-Dbes.uber.jar=$(rootpath :bes_deploy.jar)",
    ],
    tags = [
        "no-sandbox",  # need to talk to docker socket
    ],
    test_class = "nickbreen.bes.AcceptanceTestSuite",
    deps = [
        ":domain",
        ":main",
        ":support",
        ":test-support",
        "@com_google_googleapis//google/devtools/build/v1:build_java_grpc",
        "@com_google_googleapis//google/devtools/build/v1:build_java_proto",
        "@com_google_protobuf//java/core",
        "@com_google_protobuf//java/util",
        "@io_grpc_grpc_java//api",
        artifact("junit:junit"),
        artifact("org.hamcrest:hamcrest"),
        artifact("org.testcontainers:testcontainers"),
        artifact("org.postgresql:postgresql"),
        artifact("org.testcontainers:postgresql"),
    ],
)
